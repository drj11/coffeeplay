<!DOCTYPE html>
<html>
<head>
  <meta http-equiv='Content-Type' content='Type=text/html; charset=utf-8'>
  <title>CoffeePlay</title>
  <link href="style.css" rel="stylesheet" />
</head>
<body>
  <header>
    <h2 id="banner">Welcome to
      <a href="https://github.com/drj11/coffeeplay/">
        <span class="logo"><i>Coffee</i><b>Play</b></span></a>
    </h2>
  </header>
  <div id="main">
    <table id="repl"></table>
    <div id="studio"></div>
  </div>
  <footer>
    <textarea rows="3" cols="43" spellcheck="false" autofocus="autofocus" placeholder="Type some CoffeeScript and press Alt-â†µ"></textarea>
  </footer>
  <script type="text/coffeescript">
# Allow truly global exports to be defined using
# exports.thing = 'stuff'
exports = this

console.log = (stuff...) ->
  appendOutput.apply(null, stuff)
  undefined

# Keeps track of how many commands have been typed.
historyIndex = 0
historyStepsBack = 0

# A hack that means evals using indirectEval will cause
# assignments to be to the global object.  Makes "foo=7"
# work in the REPL.
indirectEval = eval

open = (obj) ->
  # Extract all of the function properties of obj and make them
  # global functions that call the corresponding method on obj.
  for k,v of obj when typeof v is 'function'
    (->
      vbind = v
      exports[k] = () -> vbind.apply(obj, arguments)
    )()
  obj

exports.Studio = (module) ->
  # Enable studio mode by displaying the '#studio' div
  # and invoking the module.  EG r = Studio Raphael
  $('#studio').css 'display', 'block'
  obj = module 'studio'
  open obj

sneakLink = (link) ->
  $("tr.source").last().after """
    <tr><td></td><td>
    <a target="newtab" href="#{link}">#{link}</a></td></tr>"""

exports.cheat = ->
  # Display link to cheatsheet.
  l = "https://github.com/drj11/flosscoffee/blob/master/cheatsheet.md"
  sneakLink l
  "see cheatsheet in link above"

exports.exercise = (n) ->
  # Display link to exercise n.
  n |= 0
  l = "https://github.com/drj11/flosscoffee/blob/master/exercise#{n}.md"
  sneakLink l
  "see exercise #{n} in link above"

repr = (x, max=6, depth=0) ->
  # Return a representation of a CoffeeScript value.
  try
    JSON.stringify x
  catch error
    if /circular/.test String error
      return "*circular*"
    return "*repr* #{String error}"
  if depth > 6
    return "*max-depth*"
  switch typeof x
    when 'string' then "'#{x.replace /'/g, "\\'"}'"
    when 'function' then '<function>'
    when 'object'
      if x is null
        return 'null'
      if Array.isArray x # Assumes ECMAScript 5
        "[#{(repr v, max, depth+1 for v in x).join ", "}]"
      else
        "{#{("#{repr k}: #{repr v, max, depth+1}" for k,v of x).join ", "}}"
    when 'undefined' then 'undefined'
    else x

compileAndGo = ->
  source = $('textarea').val()
  result = {}
  compiled = null
  appendSource(source)
  if source.match /^[^ ]/
    histSource(source)
  historyIndex += 1
  historyStepsBack = 0
  console.debug "will compile", source
  try
    compiled = CoffeeScript.compile source, bare: yes
    console.debug "compiled code", compiled
  catch error
    result.value = error
    result.completion = 'coffeeError'
  if compiled?
    try
      result.value = indirectEval compiled
      result.completion = 'normal'
    catch error
      result.value = error
      result.completion = 'error'
  appendResult result
  $('textarea').select()

history = []
histSource = (source) ->
  history.push source
  localStorage.history = JSON.stringify history

appendSource = (source) ->
  permalink = location.origin + location.pathname + '#play:' +
    encodeURIComponent source
  $tr = $('<tr class="source">')
  $tr.append("""<td class="history">#{historyIndex}</td>""")
  $td = $ '<td class="message">'
  $td.text source
  $td.append(""" <a class="permalink" href="#{permalink}">Permalink</a>""")
  $tr.append($td)
  $tr.appendTo('#repl')

appendResult = (result) ->
  switch result.completion
    when 'normal'
      c = 'result normal'
    when 'error'
      c = 'result error'
    when 'coffeeError'
      c = 'result coffee error'
  $tr = $("""<tr class="#{c}">""")
  $tr.append("""<td>""")
  $td = $ '<td class="message">'
  if result.completion is 'normal'
    $td.text repr result.value
  else
    # For error objects.
    $td.text String result.value
  $tr.append $td
  $tr.appendTo('#repl')

appendOutput = (output...) ->
  output = output.join ' '
  $tr = $('<tr class="output">')
    .append('<td>')
    .append($('<td class="message">').text(output))
    .appendTo('#repl')

snapScroll = ->
  # Scroll the document so that the repl is showing at the bottom.
  # After http://stackoverflow.com/a/5909328/242457
  $('body').scrollTop($(document).height())

loadPreviousSource = ->
  history = JSON.parse localStorage.history
  if historyStepsBack == 0
    historyStepsBack = 1
  else if historyStepsBack == history.length
    return false
  else
    historyStepsBack += 1
  console.dir historyStepsBack
  $('textarea').val history[history.length - historyStepsBack]

loadNextSource = ->
  history = JSON.parse localStorage.history
  if historyStepsBack == 0
    $('textarea').val ''
    return false
  else
    historyStepsBack -= 1
  console.dir historyStepsBack
  $('textarea').val history[history.length - historyStepsBack]

$('textarea').keydown (e) ->
    modified = e.altKey || e.ctrlKey
    if e.which == 13 and (modified or e.shiftKey)
      compileAndGo()
      snapScroll()
      e.preventDefault()
    if modified and e.which == 38
      loadPreviousSource()
      e.preventDefault()
    if modified and e.which == 40
      loadNextSource()
      e.preventDefault()

prog = decodeURIComponent window.location.hash
if prog.match /^#play:/
  prog = prog.replace /^#play:/, ''
  $('textarea').val(prog)
  compileAndGo()

# Randomly update the placeholder text
if Math.random() < 0.5
  p = $('footer textarea').attr 'placeholder'
  p = p.replace 'Alt', 'Ctrl'
  $('footer textarea').attr 'placeholder', p
  </script>
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.8.3/jquery.min.js"></script>
  <script src="http://coffeescript.org/extras/coffee-script.js"></script>
  <script src="http://cdnjs.cloudflare.com/ajax/libs/raphael/2.1.0/raphael-min.js"></script>
  <script src="http://g.raphaeljs.com/g.raphael.js"></script>
  <script src="http://g.raphaeljs.com/g.pie.js"></script>
  <script src="http://g.raphaeljs.com/g.bar.js"></script>
</body>
</html>
