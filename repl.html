<html>
<body>
<style type="text/css">
.source {
  background-color: #ccc
}
.result.error
{
  background-color: #fc2
}
</style>
<div id="repl_div">
<textarea id="repl" rows="10" spellcheck="false"
  autofocus="autofocus"></textarea>
</div>
<script type="text/coffeescript">
console.log "CoffeeScript rules!"
compileAndGo = ->
  source = $('#repl').val()
  result = {}
  compiled = null
  console.log "will compile", source
  try
    compiled = CoffeeScript.compile source, bare: yes
    console.log "compiled code", compiled
  catch error
    result.coffeeError = error
  if compiled?
    try
      result.normal = eval compiled
    catch error
      result.error = error
  appendEvaluation source, result
  $('#repl').select()

appendEvaluation = (source, result) ->
  $('#repl').before($('<p>').text(source).addClass('source'))
  appendResult result

appendResult = (result) ->
  if result.normal?
    # Normal evaluation.
    t = result.normal
    c = 'result normal'
  else if result.error?
    # Runtime error, such as missing function.
    t = result.error
    c = 'result error'
  else if result.coffeeError?
    # Error when compiling CoffeeScript, such as missing '
    t = result.coffeeError
    c = 'result coffee error'
  t ?= result
  $('#repl').before($('<p>').text(t).addClass(c))

snapScroll = ->
  # Scroll the document so that the repl is showing at the bottom.
  # After http://stackoverflow.com/a/5909328/242457
  $('body').scrollTop($(document).height())

$(document.body)
  .keydown (e) ->
    if e.which == 13
      compileAndGo()
      snapScroll()
      e.preventDefault()
</script>
<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.8.3/jquery.min.js"></script>
<script src="http://coffeescript.org/extras/coffee-script.js"></script>
</html>
