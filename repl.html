<!doctype>
<html>
<head>
  <meta http-equiv='Content-Type' content='Type=text/html; charset=utf-8'>
  <title>CoffeePlay</title>
  <link href="style.css" rel="stylesheet" />
</head>
<body>
  <header>
    <h2 id="banner">Welcome to
      <a href="https://github.com/drj11/coffeeplay/">
        <span class="logo"><i>Coffee</i><b>Play</b></span></a>
    </h2>
  </header>
  <div id="main">
    <table id="repl"></table>
    <div id="me"></div>
  </div>
  <footer>
    <textarea rows="3" cols="43" spellcheck="false" autofocus="autofocus" placeholder="Type some CoffeeScript and press â†µ"></textarea>
  </footer>
  <script type="text/coffeescript">
# Allow truly global exports to be defined using
# exports.thing = 'stuff'
exports = this

console.log = (stuff...) ->
  appendOutput.apply(null, stuff)

# Keeps track of how many commands have been typed.
historyIndex = 0

# A hack that means evals using indirectEval will cause
# assignments to be to the global object.  Makes "foo=7"
# work in the REPL.
indirectEval = eval

open = (obj) ->
  # Extract all of the function properties of obj and make them
  # global functions that call the corresponding method on obj.
  (exports[k] = (args...) -> v.apply(obj, args))  for k,v of obj when typeof v is 'function'
  # Avoids constructing and returning list.
  'opened'

exports.Studio = (module) ->
  # Enable studio mode by displaying the '#me' div
  # and invoking the module.  EG r = Studio Raphael
  $('#me').css 'display', 'block'
  obj = module 'me'
  open obj

compileAndGo = ->
  source = $('textarea').val()
  result = {}
  compiled = null
  console.debug "will compile", source
  try
    compiled = CoffeeScript.compile source, bare: yes
    console.debug "compiled code", compiled
  catch error
    result.value = error
    result.completion = 'coffeeError'
  if compiled?
    try
      result.value = indirectEval compiled
      result.completion = 'normal'
    catch error
      result.value = error
      result.completion = 'error'
  appendEvaluation source, result
  $('textarea').select()

appendEvaluation = (source, result) ->
  appendSource(source)
  historyIndex += 1
  appendResult result

appendSource = (source) ->
  permalink = location.origin + location.pathname + '#play:' +
    encodeURIComponent source
  $tr = $('<tr class="source">')
  $tr.append("""<td class="history">#{historyIndex}</td>""")
  $tr.append("""<td class="message">#{source} <a class="permalink" href="#{permalink}">Permalink</a></td>""")
  $tr.appendTo('#repl')

appendResult = (result) ->
  switch result.completion
    when 'normal'
      c = 'result normal'
    when 'error'
      c = 'result error'
    when 'coffeeError'
      c = 'result coffee error'
  $tr = $("""<tr class="#{c}">""")
  $tr.append("""<td>""")
  $tr.append("""<td class="message">#{String(result.value)}</td>""")
  $tr.appendTo('#repl')

appendOutput = (output...) ->
  output = output.join ' '
  $tr = $('<tr class="output">')
    .append('<td>')
    .append($('<td class="message">').text(output))
    .appendTo('#repl')

snapScroll = ->
  # Scroll the document so that the repl is showing at the bottom.
  # After http://stackoverflow.com/a/5909328/242457
  $('body').scrollTop($(document).height())

$(document.body)
  .keydown (e) ->
    modified = e.shiftKey
    if modified
      return true
    if e.which == 13
      compileAndGo()
      snapScroll()
      e.preventDefault()

prog = decodeURIComponent window.location.hash
if prog.match /^#play:/
  prog = prog.replace /^#play:/, ''
  $('textarea').val(prog)
  compileAndGo()
  </script>
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.8.3/jquery.min.js"></script>
  <script src="http://coffeescript.org/extras/coffee-script.js"></script>
  <script src="http://cdnjs.cloudflare.com/ajax/libs/raphael/2.1.0/raphael-min.js"></script>
  <script src="http://g.raphaeljs.com/g.raphael.js"></script>
  <script src="http://g.raphaeljs.com/g.pie.js"></script>
</body>
</html>
