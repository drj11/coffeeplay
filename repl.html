<html>
<meta http-equiv='Content-Type' content='Type=text/html; charset=utf-8'>
<body>
<style type="text/css">
h2 {
  font-weight: normal
}
.logo i {
  font-weight: normal
}
.logo b {
  font-style: bold
}
.source {
  background-color: #ccc
}
.source .history {
  background-color: #999
}
.source .history:after {
  content: ": "
}
.result.error
{
  background-color: #fc2
}
.result.coffee.error
{
  background-color: #c82
}
#me {
  display: none;
  position: fixed;
  width: 400px;
  height: 400px;
  top: 10px;
  right: 10px;
  background: rgba(220,220,220,0.8);
  margin: 0;
}
</style>
<h2 id="banner">Welcome to
<span class="logo"><i>Coffee</i><b>Play</b></span>
</h2>
<div id="main">
<div id="repl">
<textarea rows="4" cols="43" spellcheck="false"
  autofocus="autofocus"
  placeholder="Type some CoffeeScript and press â†µ"
  ></textarea>
</div>
<div id="me">
</div>
</div>
<script type="text/coffeescript">
# Allow truly global exports to be defined using
# exports.thing = 'stuff'
exports = this

# Keeps track of how many commands have been typed.
historyIndex = 0

# A hack that means evals using indirectEval will cause
# assignments to be to the global object.  Makes "foo=7"
# work in the REPL.
indirectEval = eval

open = (obj) ->
  # Extract all of the function properties of obj and make them
  # global functions that call the corresponding method on obj.
  (exports[k] = (args...) -> v.apply(obj, args))  for k,v of obj when typeof v is 'function'
  # Avoids constructing and returning list.
  'opened'

exports.Studio = (module) ->
  # Enable studio mode by displaying the '#me' div
  # and invoking the module.  EG r = Studio Raphael
  $('#me').css 'display', 'block'
  obj = module 'me'
  open obj

compileAndGo = ->
  source = $('#repl textarea').val()
  result = {}
  compiled = null
  console.log "will compile", source
  try
    compiled = CoffeeScript.compile source, bare: yes
    console.log "compiled code", compiled
  catch error
    result.value = error
    result.completion = 'coffeeError'
  if compiled?
    try
      result.value = indirectEval compiled
      result.completion = 'normal'
    catch error
      result.value = error
      result.completion = 'error'
  appendEvaluation source, result
  $('#repl textarea').select()

appendEvaluation = (source, result) ->
  appendSource(source)
  historyIndex += 1
  appendResult result

appendSource = (source) ->
  permalink = location.origin + location.pathname + '#play:' +
    encodeURIComponent source
  $a = $('<a>').attr('href', permalink).text('permalink')
  $('#repl textarea').before($('<p>').text(source).addClass('source')
    .append($('<span>').addClass('permalink').text(' ').append($a))
    .prepend("<span class='history'>#{historyIndex}</span>"))

appendResult = (result) ->
  switch result.completion
    when 'normal'
      c = 'result normal'
    when 'error'
      c = 'result error'
    when 'coffeeError'
      c = 'result coffee error'
  $('#repl textarea').before($('<p>').text(String(result.value)).addClass(c))

snapScroll = ->
  # Scroll the document so that the repl is showing at the bottom.
  # After http://stackoverflow.com/a/5909328/242457
  $('body').scrollTop($(document).height())

$(document.body)
  .keydown (e) ->
    modified = e.shiftKey
    if modified
      return true
    if e.which == 13
      compileAndGo()
      snapScroll()
      e.preventDefault()

prog = decodeURIComponent window.location.hash
if prog.match /^#play:/
  prog = prog.replace /^#play:/, ''
  $('#repl textarea').val(prog)
  compileAndGo()
</script>
<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.8.3/jquery.min.js"></script>
<script src="http://coffeescript.org/extras/coffee-script.js"></script>
<script src="http://cdnjs.cloudflare.com/ajax/libs/raphael/2.1.0/raphael-min.js"></script>
<script src="http://g.raphaeljs.com/g.raphael.js"></script>
<script src="http://g.raphaeljs.com/g.pie.js"></script>
</html>
