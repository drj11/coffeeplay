<html>
<body>
<style type="text/css">
.source {
  background-color: #ccc
}
.source .history {
  background-color: #999
}
.source .history:after {
  content: ": "
}
.result.error
{
  background-color: #fc2
}
</style>
<div id="repl_div">
<textarea id="repl" rows="10" spellcheck="false"
  autofocus="autofocus"></textarea>
</div>
<script type="text/coffeescript">
# Keeps track of how many commands have been typed.
historyIndex = 0
compileAndGo = ->
  source = $('#repl').val()
  result = {}
  compiled = null
  console.log "will compile", source
  try
    compiled = CoffeeScript.compile source, bare: yes
    console.log "compiled code", compiled
  catch error
    result.value = error
    result.completion = 'coffeeError'
  if compiled?
    try
      result.value = eval compiled
      result.completion = 'normal'
    catch error
      result.value = error
      result.completion = 'error'
  appendEvaluation source, result
  $('#repl').select()

appendEvaluation = (source, result) ->
  $('#repl').before($('<p>').text(source).addClass('source')
    .prepend("<span class='history'>#{historyIndex}</span>"))
  historyIndex += 1
  appendResult result

appendResult = (result) ->
  switch result.completion
    when 'normal'
      c = 'result normal'
    when 'error'
      c = 'result error'
    when 'coffeeError'
      c = 'result coffee error'
  $('#repl').before($('<p>').text(result.value).addClass(c))

snapScroll = ->
  # Scroll the document so that the repl is showing at the bottom.
  # After http://stackoverflow.com/a/5909328/242457
  $('body').scrollTop($(document).height())

$(document.body)
  .keydown (e) ->
    if e.which == 13
      compileAndGo()
      snapScroll()
      e.preventDefault()
</script>
<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.8.3/jquery.min.js"></script>
<script src="http://coffeescript.org/extras/coffee-script.js"></script>
</html>
